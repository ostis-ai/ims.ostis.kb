agent_of_finding_relation
=> nrel_main_idtf:
	[агентная scp-программа поиска отношений, заданных на понятии] (* <- lang_ru;; *);
	[agent scp-program of finding relations, defined for given concept] (* <- lang_en;; *);
	<- agent_scp_program;;

scp_program -> agent_of_finding_relation 
	(*
	-> rrel_params: .agent_of_finding_relation_params 
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	-> rrel_operators: .agent_of_finding_relation_operator_set 
		(*
		-> rrel_init: .agent_of_finding_relation_operator0A 
			(*
		  	<- searchElStr3;;
		  	-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
		  	-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

		  	=> nrel_goto: .agent_of_finding_relation_operator0B;;
			*);;

		 -> .agent_of_finding_relation_operator0B 
			(*
		  	<- searchElStr3;;
		  	-> rrel_1: rrel_fixed: rrel_scp_const: question_finding_relation;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

		  	=> nrel_then: .agent_of_finding_relation_operator0C;;
		  	=> nrel_else: .agent_of_finding_relation_operator_return;;
			*);;    

		-> .agent_of_finding_relation_operator0C
			(*
		 	<- searchElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _connection;;
		
                 	=> nrel_then: .agent_of_finding_relation_operator0D;;
                 	=> nrel_else: .agent_of_finding_relation_operator_return;;
                 	*);; 

                -> .agent_of_finding_relation_operator0D 
			(*
		  	<- genEl;;
		  	-> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _answer;;
		 
		 	=> nrel_goto: .agent_of_finding_relation_operator1A;;
                 	*);;
	
	        -> .agent_of_finding_relation_operator1A 
			(*
			<- searchSetStr3;;
			-> rrel_1: rrel_assign: rrel_scp_var: _connect;;
                        -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc_of_perebor;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _connection;;

			-> rrel_set_1: rrel_assign: rrel_scp_var: _temp;;

			=> nrel_then: .agent_of_finding_relation_operator1B;;
			=> nrel_else: .agent_of_finding_relation_operator4A;;
			*);;

		-> .agent_of_finding_relation_operator1B 
			(*
		 	<- searchElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _temp;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc_of_perebor;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _may_be_connect;;

		 	=> nrel_then: .agent_of_finding_relation_operator1C;;
                 	=> nrel_else: .agent_of_finding_relation_operator4A;;
		 	*);; 

		-> .agent_of_finding_relation_operator1C 
			(*
		 	<- eraseEl;;
		 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc_of_perebor;;

                 	=> nrel_goto: .agent_of_finding_relation_operator1D;;
		 	*);;

		-> .agent_of_finding_relation_operator1D 
			(*
		 	<- searchElStr5;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _may_be_connect;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _node;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_combination;;

                 	=> nrel_then: .agent_of_finding_relation_operator1E;;
		 	=> nrel_else: .agent_of_finding_relation_operator1B;;
                 	*);;

		-> .agent_of_finding_relation_operator1E 
			(*
		 	<- searchElStr5;;
		 	-> rrel_1: rrel_assign: rrel_scp_var: _inst;;
		 	-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _node;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_definitional_domain;;

                 	=> nrel_then: .agent_of_finding_relation_operator1B;;
		 	=> nrel_else: .agent_of_finding_relation_operator2A;;
                 	*);;

		-> .agent_of_finding_relation_operator2A 
			(*
		 	<- searchSetStr5;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _may_be_connect;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _node;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_combination;;

		 	-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_set_3: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_set_4: rrel_fixed: rrel_scp_var: _answer;;

                 	=> nrel_then: .agent_of_finding_relation_operator2B;;
		 	=> nrel_else: .agent_of_finding_relation_operator1B;;
                 	*);;

		-> .agent_of_finding_relation_operator2B 
			(*
			<- searchSetStr5;;
		 	-> rrel_1: rrel_assign: rrel_scp_var: _smth;;
		 	-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _node;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_first_domain;;
	         
                	-> rrel_set_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_set_4: rrel_fixed: rrel_scp_var: _answer;;

                 	=> nrel_then: .agent_of_finding_relation_operator3A;;
                 	=> nrel_else: .agent_of_finding_relation_operator2C;;
                 	*);;

		-> .agent_of_finding_relation_operator2C 
			(*
		 	<- searchSetStr5;;
		 	-> rrel_1: rrel_assign: rrel_scp_var: _smth;;
		 	-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _node;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_second_domain;;
	         
                 	-> rrel_set_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_set_4: rrel_fixed: rrel_scp_var: _answer;;

                 	=> nrel_then: .agent_of_finding_relation_operator3A;;
                 	=> nrel_else: .agent_of_finding_relation_operator2D;;
                 	*);;

		-> .agent_of_finding_relation_operator2D 
			(*
		 	<- searchSetStr5;;
		 	-> rrel_1: rrel_assign: rrel_scp_var: _smth;;
		 	-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _node;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_third_domain;;
	         
                	-> rrel_set_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_set_4: rrel_fixed: rrel_scp_var: _answer;;

                 	=> nrel_then: .agent_of_finding_relation_operator3A;;
		 	=> nrel_else: .agent_of_finding_relation_operator1B;;
                 	*);; 

		-> .agent_of_finding_relation_operator3A 
			(*
		 	<- genElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _may_be_connect;;

                 	=> nrel_goto: .agent_of_finding_relation_operator3B;;
		 	*);;

		-> .agent_of_finding_relation_operator3B 
			(*
		 	<- searchSetStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _may_be_connect;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _connection;;
	         
		 	-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;

                 	=> nrel_goto: .agent_of_finding_relation_operator1B;;
                 	*);;

		-> .agent_of_finding_relation_operator4A 
			(*
		 	<- searchSetStr5;;
		 	-> rrel_1: rrel_assign: rrel_scp_var: _el1;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _connection;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_first_domain;;
	         
                 	-> rrel_set_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_set_4: rrel_fixed: rrel_scp_var: _answer;;

                 	=> nrel_then: .agent_of_finding_relation_operator4B;;
                 	=> nrel_else: .agent_of_finding_relation_operator4B;;
                 	*);; 

		-> .agent_of_finding_relation_operator4B 
			(*
		 	<- searchSetStr5;;	
		 	-> rrel_1: rrel_assign: rrel_scp_var: _el2;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _connection;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_second_domain;;
	         
                 	-> rrel_set_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_set_4: rrel_fixed: rrel_scp_var: _answer;;

                 	=> nrel_then: .agent_of_finding_relation_operator4C;;
                 	=> nrel_else: .agent_of_finding_relation_operator4C;;
                 	*);;
		
		-> .agent_of_finding_relation_operator4C 
			(*
		 	<- searchSetStr5;;
		 	-> rrel_1: rrel_assign: rrel_scp_var: _el3;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _connection;;	
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_third_domain;;
	         
                 	-> rrel_set_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_set_4: rrel_fixed: rrel_scp_var: _answer;;

                 	=> nrel_goto: .agent_of_finding_relation_operator5A;;
                 	*);;

		-> .agent_of_finding_relation_operator5A 
			(*
		 	<- genElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_const: nrel_first_domain;;

                 	=> nrel_goto: .agent_of_finding_relation_operator5B;;
		 	*);;

		-> .agent_of_finding_relation_operator5B 
			(*
		 	<- genElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_const: nrel_second_domain;;

                 	=> nrel_goto: .agent_of_finding_relation_operator5C;;
		 	*);;

		-> .agent_of_finding_relation_operator5C 
			(*
		 	<- genElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_const: nrel_third_domain;;

                 	=> nrel_goto: .agent_of_finding_relation_operator5D;;
		 	*);;

		-> .agent_of_finding_relation_operator5D 
			(*
		 	<- genElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_const: nrel_combination;;

                 	=> nrel_goto: .agent_of_finding_relation_operator5E;;
		 	*);;

		-> .agent_of_finding_relation_operator5E 
			(*
		 	<- genElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _connection;;

                 	=> nrel_goto: .agent_of_finding_relation_gen_answer;;
		 	*);;

                  -> .agent_of_finding_relation_gen_answer 
			(*
		  	<- genElStr5;;
		  	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
		  	-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;

		  	=> nrel_goto: .agent_of_finding_relation_operator_return;;
			*);;
					
		 -> .agent_of_finding_relation_operator_return 
			(*
		  	<- return;;
			*);;
		*);;
	*);;

