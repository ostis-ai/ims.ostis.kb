scp_program -> proc_search_all_input (*	
	-> rrel_params: .proc_search_all_input_params (*
		-> rrel_1: rrel_in: _parameter;;
		-> rrel_2: rrel_in: _answer;;
	*);;
	
	-> rrel_operators: .proc_search_all_input_operator_set (*

		-> rrel_init: .proc_search_all_input_operator1 (*
		  <- genEl;;
		  -> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _set_elem;;

		  => nrel_goto: .proc_search_all_input_operator3;;
		*);;
		
		-> .proc_search_all_input_operator3 (*
		  <- searchSetStr3;;
		  -> rrel_1: rrel_assign: rrel_scp_var: _elem;;
		  -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  -> rrel_3: rrel_fixed: rrel_scp_var: _parameter;;

		  -> rrel_set_1: rrel_fixed: rrel_scp_var: _set_elem;;
		  -> rrel_set_2: rrel_fixed: rrel_scp_var: _set_elem;;

		  => nrel_then: .proc_search_all_input_operator4;;
		  => nrel_else: .proc_search_all_input_operator_return;;
		*);;

		/* Elements processing cycle */		 
		-> .proc_search_all_input_operator4 (*
		  <- searchElStr3;;
		  -> rrel_1: rrel_fixed: rrel_scp_var: _set_elem;;
		  -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  -> rrel_3: rrel_assign: rrel_scp_var: _curr_elem;;

		  => nrel_then: .proc_search_all_input_operator5;;
		  => nrel_else: .proc_search_all_input_operator7_5;;
		*);;

		-> .proc_search_all_input_operator5 (*
		  <- eraseEl;;
		  -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc1;;

		  => nrel_goto: .proc_search_all_input_operator6;;
		*);;

		-> .proc_search_all_input_operator6 (*
		  <- searchElStr3;;
		  -> rrel_1: rrel_fixed: rrel_scp_const: system_element;;
		  -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  -> rrel_3: rrel_fixed: rrel_scp_var: _curr_elem;;

		  => nrel_then: .proc_search_all_input_operator4;;
		  => nrel_else: .proc_search_all_input_operator6_1;;
		*);;

		-> .proc_search_all_input_operator6_1 (*
		  <- searchElStr3;;
		  -> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  -> rrel_3: rrel_fixed: rrel_scp_var: _curr_elem;;

		  => nrel_then: .proc_search_all_input_operator4;;
		  => nrel_else: .proc_search_all_input_operator7;;
		*);;

		-> .proc_search_all_input_operator7 (*
		  <- genElStr3;;
		  -> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  -> rrel_3: rrel_fixed: rrel_scp_var: _curr_elem;;

		  => nrel_goto: .proc_search_all_input_operator4;;
		*);;

		-> .proc_search_all_input_operator7_5 (*
		  <- genElStr3;;
		  -> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  -> rrel_3: rrel_fixed: rrel_scp_var: _parameter;;

		  => nrel_goto: .proc_search_all_input_operator8;;
		*);;

		-> .proc_search_all_input_operator_print_result (*
		  <- printEl;;
		  -> rrel_1: rrel_fixed: rrel_scp_var: _answer;;

		  => nrel_goto: .proc_search_all_input_operator_return;;
		*);;

		/* Garbage cleaning */
		-> .proc_search_all_input_operator8 (*
		  <- eraseEl;;
		  -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _set_elem;;

		  => nrel_goto: .proc_search_all_input_operator_return;;
		*);;
					
		-> .proc_search_all_input_operator_return (*
		  <- return;;
		*);;
	*);;
*);;
