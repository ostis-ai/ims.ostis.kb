scp_program -> proc_of_finding_identifier 
	(*	
	-> rrel_params: .proc_of_finding_identifier_params (*
		-> rrel_1: rrel_in: _elem;;
		-> rrel_2: rrel_in: _answer;;
	*);;

       -> rrel_operators: .proc_of_finding_identifier_operator_set 
		(*
		-> rrel_init: .proc_of_finding_identifier_operator1A
			(*
		 	<- searchSetStr5;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _elem;;
		 	-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _elem2;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_main_idtf;;
	         
                 	-> rrel_set_3: rrel_assign: rrel_scp_var: _mainset;;

                 	=> nrel_then: .proc_of_finding_identifier_operator1B;;
			=> nrel_else: .proc_of_finding_identifier_operator_return;;
                 	*);;

		-> .proc_of_finding_identifier_operator1B 
			(*
		 	<- searchElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _mainset;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _idtf;;

		 	=> nrel_then: .proc_of_finding_identifier_operator1C;;
                 	=> nrel_else: .proc_of_finding_identifier_operator4;;
		 	*);; 

		-> .proc_of_finding_identifier_operator1C 
			(*
		 	<- eraseEl;;
		 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc2;;

                 	=> nrel_goto: .proc_of_finding_identifier_operator1D;;
		 	*);;

		-> .proc_of_finding_identifier_operator1D 
			(*
			<- genElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _idtf;;

                 	=> nrel_goto: .proc_of_finding_identifier_operator2A;;
		 	*);;

		-> .proc_of_finding_identifier_operator2A
			(*
		 	<- searchSetStr5;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _elem;;
		 	-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _idtf;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_main_idtf;;
	         
                 	-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_set_4: rrel_fixed: rrel_scp_var: _answer;;

                 	=> nrel_then: .proc_of_finding_identifier_operator2B;;
			=> nrel_else: .proc_of_finding_identifier_operator_return;;
                 	*);;

		-> .proc_of_finding_identifier_operator2B 
			(*
			<- searchSetStr3;;
		 	-> rrel_1: rrel_assign: rrel_scp_var: _lang;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _idtf;;

                 	-> rrel_set_1: rrel_assign: rrel_scp_var: _tempset;;

		 	=> nrel_then: .proc_of_finding_identifier_operator3A;;
			=> nrel_else: .proc_of_finding_identifier_operator_return;;
		 	*);;  

		-> .proc_of_finding_identifier_operator3A 
			(*
		 	<- searchElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _tempset;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _lang2;;

		 	=> nrel_then: .proc_of_finding_identifier_operator3B;;
                 	=> nrel_else: .proc_of_finding_identifier_operator1B;;
		 	*);; 

		-> .proc_of_finding_identifier_operator3B 
			(*
			<- eraseEl;;
		 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc;;

                 	=> nrel_goto: .proc_of_finding_identifier_operator3C;;
		 	*);;
  
		-> .proc_of_finding_identifier_operator3C 
			(*
		 	<- searchElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_const: languages;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _lang2;;

		 	=> nrel_then: .proc_of_finding_identifier_operator3D;;
                 	=> nrel_else: .proc_of_finding_identifier_operator3A;;
		 	*);;

		-> .proc_of_finding_identifier_operator3D 
			(*
		 	<- genElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _lang2;;
               
		  	=> nrel_goto: .proc_of_finding_identifier_operator3E;;
		  	*);;

		-> .proc_of_finding_identifier_operator3E 
			(*
		 	<- searchSetStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _lang2;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _idtf;;

                 	-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;

	         	=> nrel_then: .proc_of_finding_identifier_operator3A;;
			=> nrel_else: .proc_of_finding_identifier_operator_return;;
                 *);;

		-> .proc_of_finding_identifier_operator4 
			(*
		 	<- genElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_const: nrel_main_idtf;;

                 	=> nrel_goto: .proc_of_finding_identifier_operator5;;
		 	*);;
		 	
		 -> .proc_of_finding_identifier_operator5 
			(*
		 	<- eraseEl;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _tempset;;

                 	=> nrel_goto: .proc_of_finding_identifier_operator6;;
		 	*);;
		 	
		 -> .proc_of_finding_identifier_operator6 
			(*
		 	<- eraseEl;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _mainset;;

                 	=> nrel_goto: .proc_of_finding_identifier_operator_return;;
		 	*);;

		-> .proc_of_finding_identifier_operator_return 
			(*
			<- return;;
			*);;
		*);;
	*);;
