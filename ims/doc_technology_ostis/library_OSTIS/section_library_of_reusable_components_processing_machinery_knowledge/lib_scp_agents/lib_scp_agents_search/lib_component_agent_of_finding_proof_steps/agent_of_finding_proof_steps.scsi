agent_of_finding_proof_steps
=> nrel_main_idtf:
	[агентная scp-программа поиска действий, используемых в рамках решения] 
	(* <- lang_ru;; *);

	[agent scp-program of finding actions, which are used within solution] 
	(* <- lang_en;; *);

<- agent_scp_program;;

scp_program -> agent_of_finding_proof_steps 
	(*
	-> rrel_params: .agent_of_finding_proof_steps_params 
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	-> rrel_operators: .agent_of_finding_proof_steps_operator_set 
		(*		
		-> rrel_init: .agent_of_finding_proof_steps_operator1A
			(*
		  	<- searchElStr3;;
		  	-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
		  	-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

		 	=> nrel_goto: .agent_of_finding_proof_steps_operator1B;;
			*);;

		-> .agent_of_finding_proof_steps_operator1B 
			(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: question_finding_proof_steps;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

		  	=> nrel_then: .agent_of_finding_proof_steps_operator1C;;
		  	=> nrel_else: .agent_of_finding_proof_steps_operator_return;;
			*);;        

		-> .agent_of_finding_proof_steps_operator1C
			(*
			<- searchElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _connection;;
		
                 	=> nrel_then: .agent_of_finding_proof_steps_operator1D;;
                 	=> nrel_else: .agent_of_finding_proof_steps_operator_return;;
                 	*);; 

		-> .agent_of_finding_proof_steps_operator1D 
			(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _answer;;
		 
			=> nrel_goto: .agent_of_finding_proof_steps_operator2A;;
                 	*);;

		-> .agent_of_finding_proof_steps_operator2A
			(*
			<- searchElStr5;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _connection;;
		 	-> rrel_2: rrel_assign:  rrel_common: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _elem;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_main_proof;;
	         
                 	=> nrel_then: .agent_of_finding_proof_steps_operator2B;;
		 	=> nrel_else: .agent_of_finding_proof_steps_operator_return;;
                 	*);;
                 	
                 -> .agent_of_finding_proof_steps_operator2B 
			(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _connection;;

		  	=> nrel_goto: .agent_of_finding_proof_steps_operator2C;;
			*);;

		-> .agent_of_finding_proof_steps_operator2C
			(*
			<- genElStr3;;
		  	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_fixed: rrel_scp_const: nrel_main_statements;;

		  	=> nrel_goto: .agent_of_finding_proof_steps_operator2D1;;
			*);;

		-> .agent_of_finding_proof_steps_operator2D1
			(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _gen;;
		 
			=> nrel_goto: .agent_of_finding_proof_steps_operator2D2;;
                 	*);;

		-> .agent_of_finding_proof_steps_operator2D2
			(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _gen;;

		  	=> nrel_goto: .agent_of_finding_proof_steps_operator2E;;
			*);;

		-> .agent_of_finding_proof_steps_operator2E 
			(*
			<- searchElStr5;;
		 	-> rrel_1: rrel_assign: rrel_scp_var: _start_vertex;;
		 	-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _elem;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_decomposition_of_sc_action;;

                 	=> nrel_then: .agent_of_finding_proof_steps_operator3A;;
		 	=> nrel_else: .agent_of_finding_proof_steps_operator_return;;
                 	*);;

		-> .agent_of_finding_proof_steps_operator3A 
			(*
			<- searchSetStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _start_vertex;;
		 	-> rrel_2: rrel_assign:  rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _param;;
                 
              	 	-> rrel_set_3: rrel_assign: rrel_scp_var: _combinations;;

                	=> nrel_then: .agent_of_finding_proof_steps_operator3B;;
		 	=> nrel_else: .agent_of_finding_proof_steps_operator_return;;
                 	*);;

		-> .agent_of_finding_proof_steps_operator3B
			(*
			<- searchElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _combinations;;
		 	-> rrel_2: rrel_assign:  rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _smth;;
                 
                 	=> nrel_then: .agent_of_finding_proof_steps_operator3C;;
		 	=> nrel_else: .agent_of_finding_proof_steps_operator4A;;
                 	*);;

		-> .agent_of_finding_proof_steps_operator3C 
			(*
			<- eraseEl;;
			-> rrel_1: rrel_erase: rrel_fixed: rrel_scp_var: _arc1;;

		 	=> nrel_goto: .agent_of_finding_proof_steps_operator3D;;
		 	*);;

		->.agent_of_finding_proof_steps_operator3D 
			(*
			<- searchElStr5;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _smth;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _elem2;;
                	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_2;;

                 	=> nrel_then: .agent_of_finding_proof_steps_operator3F1;;
		 	=> nrel_else: .agent_of_finding_proof_steps_operator3B;;
			*);;

		-> .agent_of_finding_proof_steps_operator3F1
			(*
			<- searchElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _gen;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _elem2;;

                 	=> nrel_else: .agent_of_finding_proof_steps_operator3F2;;
		 	=> nrel_then: .agent_of_finding_proof_steps_operator3B;;
			*);;

		-> .agent_of_finding_proof_steps_operator3F2 
			(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _elem2;;

                 	=> nrel_goto: .agent_of_finding_proof_steps_operator3F3;;
		 	*);;

		-> .agent_of_finding_proof_steps_operator3F3 
			(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _gen;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _nessecary_arc;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _elem2;;

                 	=> nrel_goto: .agent_of_finding_proof_steps_operator3F4;;
		 	*);;

		-> .agent_of_finding_proof_steps_operator3F4 
			(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _nessecary_arc;;

                 	=> nrel_goto: .agent_of_finding_proof_steps_operator3B;;
		 	*);;		
		
		-> .agent_of_finding_proof_steps_operator4A
			(*
			<- genElStr5;;
		  	-> rrel_1: rrel_fixed: rrel_scp_var: _gen;;
		  	-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _connection;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_main_statements;;

		  	=> nrel_goto: .agent_of_finding_proof_steps_operator4B;;
			*);;

		-> .agent_of_finding_proof_steps_operator4B
			(*
			<- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _gen;;
		 	-> rrel_2: rrel_assign:  rrel_common: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _connection;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_main_statements;;

			-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;; 
			-> rrel_set_4: rrel_fixed: rrel_scp_var: _answer;; 

                	=> nrel_goto: .agent_of_finding_proof_steps_gen_answer;;
                 	*);;

		-> .agent_of_finding_proof_steps_gen_answer 
			(*
			<- genElStr5;;
		  	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
		  	-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;

		  	=> nrel_goto: .agent_of_finding_proof_steps_operator_return;;
			*);;
					
		-> .agent_of_finding_proof_steps_operator_return 
			(*
			<- return;;
			*);;
		*);;
	*);;

