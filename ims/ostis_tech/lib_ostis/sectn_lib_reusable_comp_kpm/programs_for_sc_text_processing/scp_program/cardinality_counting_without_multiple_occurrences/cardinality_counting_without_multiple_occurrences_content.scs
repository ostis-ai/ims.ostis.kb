lib_component_proc_of_cardinality_counting_without_multiple_occurrences =  [*
proc_of_cardinality_counting_without_multiple_occurrences
=> nrel_main_idtf:
	[scp-программа подсчёта мощности множества без учёта кратных вхождений] (* <- lang_ru;; *);
	[scp-program of set's cardinality counting without taking into account multiple occurrences] (* <- lang_en;; *);
<- scp_program;
-> rrel_key_sc_element: .._process1;;

proc_of_cardinality_counting_without_multiple_occurrences = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _set;
_-> rrel_2:: rrel_out:: _cardinality;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator1A (*
		_<- contAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _cardinality;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [0];;

		_=> nrel_goto:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator1B;;
	*);;

	_-> .._proc_of_cardinality_counting_without_multiple_occurrences_operator1B (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _set;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _element;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_3:: _set_of_elements;;

		_=> nrel_then:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator1C;;
		_=> nrel_else:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator_return;;
	*);;

	_-> .._proc_of_cardinality_counting_without_multiple_occurrences_operator1C (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _checked_elements;;

		_=> nrel_goto:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator2A;;
	*);;

	_-> .._proc_of_cardinality_counting_without_multiple_occurrences_operator_return (*
		_<- return;;

	*);;

	_-> .._proc_of_cardinality_counting_without_multiple_occurrences_operator2A (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _set_of_elements;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _element;;

		_=> nrel_then:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator2B;;
		_=> nrel_else:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator3A;;
	*);;

	_-> .._proc_of_cardinality_counting_without_multiple_occurrences_operator2B (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator2C;;
	*);;

	_-> .._proc_of_cardinality_counting_without_multiple_occurrences_operator3A (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _set_of_elements;;

		_=> nrel_goto:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator3B;;
	*);;

	_-> .._proc_of_cardinality_counting_without_multiple_occurrences_operator3B (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _checked_elements;;

		_=> nrel_goto:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator_return;;
	*);;

	_-> .._proc_of_cardinality_counting_without_multiple_occurrences_operator2C (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _checked_elements;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _element;;

		_=> nrel_then:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator2A;;
		_=> nrel_else:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator2D;;
	*);;

	_-> .._proc_of_cardinality_counting_without_multiple_occurrences_operator2D (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _checked_elements;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _element;;

		_=> nrel_goto:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator2E;;
	*);;

	_-> .._proc_of_cardinality_counting_without_multiple_occurrences_operator2E (*
		_<- contAdd;;
		_-> rrel_1:: rrel_2:: rrel_fixed:: rrel_scp_var:: _cardinality;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: [1];;

		_=> nrel_goto:: .._proc_of_cardinality_counting_without_multiple_occurrences_operator2A;;
	*);;

*);;

*];;
*];;
