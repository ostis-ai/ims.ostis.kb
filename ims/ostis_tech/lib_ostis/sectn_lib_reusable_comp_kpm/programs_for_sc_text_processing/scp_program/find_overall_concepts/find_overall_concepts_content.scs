lib_component_proc_finding_overall_concepts =  [*
proc_finding_overall_concepts
<- scp_program;
-> rrel_key_sc_element: .._process1;;

proc_finding_overall_concepts = [*
.._process1
<-_ scp_process;

_-> rrel_1:: rrel_in:: _rules;
_-> rrel_2:: rrel_in:: _rules2;
_-> rrel_3:: rrel_in:: _rules3;
_-> rrel_4:: rrel_in:: _result_proc_finding_overall_concepts;
_-> rrel_5:: rrel_in:: _el1;
_-> rrel_6:: rrel_in:: _el2;
<=_ nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._proc_finding_overall_concepts_operator1A (*
		<-_ call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_equivalent_elements;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._proc_finding_overall_concepts_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _rules;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _rules2;;
			_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _rules3;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._proc_finding_overall_concepts_operator1B;;
	*);;

	_-> .._proc_finding_overall_concepts_operator1B (*
		<-_ waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._proc_finding_overall_concepts_operator2A;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2A (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _rules3;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _node;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator2B;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator6A;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2B (*
		<-_ eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._proc_finding_overall_concepts_operator2C1;;
	*);;

	_-> .._proc_finding_overall_concepts_operator6A (*
		<-_ eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_const:: _rules;;

		_=> nrel_goto:: .._proc_finding_overall_concepts_operator6B;;
	*);;

	_-> .._proc_finding_overall_concepts_operator6B (*
		<-_ eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_const:: _rules2;;

		_=> nrel_goto:: .._proc_finding_overall_concepts_operator6C;;
	*);;

	_-> .._proc_finding_overall_concepts_operator6C (*
		<-_ eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_const:: _rules3;;

		_=> nrel_goto:: .._proc_finding_overall_concepts_operator_return;;
	*);;

	_-> .._proc_finding_overall_concepts_operator_return (*
		<-_ return;;

	*);;

	_-> .._proc_finding_overall_concepts_operator2C1 (*
		<-_ searchElStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _d1;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_4:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_studied_relation;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator2C2;;
	*);;

	_-> .._proc_finding_overall_concepts_operator3D (*
		<-_ genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _result_proc_finding_overall_concepts;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _node;;

		_=> nrel_goto:: .._proc_finding_overall_concepts_operator4A;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2C2 (*
		<-_ searchElStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _d2;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_4:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_studied_object_class;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator2C3;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2C3 (*
		<-_ searchElStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _d3;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_4:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_maximum_studied_object_class;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator2C4;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2C4 (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: subject_domain;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _node;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator2C5;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2C5 (*
		<-_ ifCoin;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: illustration;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _node;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator2C6;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2C6 (*
		<-_ ifCoin;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: sc_text_definition;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _node;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator2C7;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2C7 (*
		<-_ ifCoin;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: explanation;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _node;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator2C8;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2C8 (*
		<-_ ifCoin;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: statement;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _node;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator2C89;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2C89 (*
		<-_ ifCoin;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: sc_relation;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _node;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator2C9;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2C9 (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: languages;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _node;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator2C10;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2C10 (*
		<-_ ifCoin;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: subject_domain;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _node;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator2D;;
	*);;

	_-> .._proc_finding_overall_concepts_operator2D (*
		<-_ searchSetStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _elem;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_1:: _sub_dom;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3A;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator_return;;
	*);;

	_-> .._proc_finding_overall_concepts_operator3A (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _sub_dom;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _inst;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3B;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator5E;;
	*);;

	_-> .._proc_finding_overall_concepts_operator3B (*
		<-_ eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._proc_finding_overall_concepts_operator3C1;;
	*);;

	_-> .._proc_finding_overall_concepts_operator5E (*
		<-_ ifVarAssign;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _sub_dom;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator5E1;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator2A;;
	*);;

	_-> .._proc_finding_overall_concepts_operator5E1 (*
		<-_ eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _sub_dom;;

		_=> nrel_goto:: .._proc_finding_overall_concepts_operator2A;;
	*);;

	_-> .._proc_finding_overall_concepts_operator3C1 (*
		<-_ searchElStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _subject_domain1;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _inst;;
		_-> rrel_4:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_studied_relation;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator3C2;;
	*);;

	_-> .._proc_finding_overall_concepts_operator3C2 (*
		<-_ searchElStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _subject_domain2;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _inst;;
		_-> rrel_4:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_studied_object_class;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator3C3;;
	*);;

	_-> .._proc_finding_overall_concepts_operator3C3 (*
		<-_ searchElStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _subject_domain3;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _inst;;
		_-> rrel_4:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_maximum_studied_object_class;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator3D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator3A;;
	*);;

	_-> .._proc_finding_overall_concepts_operator4A (*
		<-_ searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _el1;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_2:: _result_proc_finding_overall_concepts;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator4B;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator4C;;
	*);;

	_-> .._proc_finding_overall_concepts_operator4B (*
		<-_ searchSetStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _rel;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc3;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _arc2;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_1:: _result_proc_finding_overall_concepts;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_2:: _result_proc_finding_overall_concepts;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator5B;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator5A;;
	*);;

	_-> .._proc_finding_overall_concepts_operator4C (*
		<-_ searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _el1;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_2:: _result_proc_finding_overall_concepts;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator4D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator5E;;
	*);;

	_-> .._proc_finding_overall_concepts_operator4D (*
		<-_ searchSetStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _rel;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc3;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _arc2;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_1:: _result_proc_finding_overall_concepts;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_2:: _result_proc_finding_overall_concepts;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator5D;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator5C;;
	*);;

	_-> .._proc_finding_overall_concepts_operator5D (*
		<-_ searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _el2;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc3;;
		_-> rrel_5:: rrel_assign:: rrel_scp_var:: _rel2;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_2:: _result_proc_finding_overall_concepts;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_4:: _result_proc_finding_overall_concepts;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_5:: _result_proc_finding_overall_concepts;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator5E;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator_return;;
	*);;

	_-> .._proc_finding_overall_concepts_operator5C (*
		<-_ searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _el2;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_2:: _result_proc_finding_overall_concepts;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator5E;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator_return;;
	*);;

	_-> .._proc_finding_overall_concepts_operator5B (*
		<-_ searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _el2;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc3;;
		_-> rrel_5:: rrel_assign:: rrel_scp_var:: _rel2;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_2:: _result_proc_finding_overall_concepts;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_4:: _result_proc_finding_overall_concepts;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_5:: _result_proc_finding_overall_concepts;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator5E;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator_return;;
	*);;

	_-> .._proc_finding_overall_concepts_operator5A (*
		<-_ searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _el2;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_2:: _result_proc_finding_overall_concepts;;

		_=> nrel_then:: .._proc_finding_overall_concepts_operator5E;;
		_=> nrel_else:: .._proc_finding_overall_concepts_operator_return;;
	*);;

*);;

*];;
*];;
