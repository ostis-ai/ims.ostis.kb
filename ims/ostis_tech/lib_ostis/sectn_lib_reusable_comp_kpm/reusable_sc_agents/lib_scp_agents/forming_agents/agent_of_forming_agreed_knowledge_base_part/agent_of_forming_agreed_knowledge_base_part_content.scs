agent_of_forming_agreed_knowledge_base_part
=> nrel_main_idtf:
	[агентная scp-программа формирования согласованной части базы знаний] (* <- lang_ru;; *);
	[agent scp-program of forming agreed knowledge base part] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_forming_agreed_knowledge_base_part = [*
.._process1
<-_ scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
<=_ nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_forming_agreed_knowledge_base_part_operator_init1 (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _action;;

		_=> nrel_goto:: .._agent_of_forming_agreed_knowledge_base_part_operator_init2;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_init2 (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_of_forming_agreed_knowledge_base_part;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action;;

		_=> nrel_then:: .._agent_of_forming_agreed_knowledge_base_part_operator_init3;;
		_=> nrel_else:: .._agent_of_forming_agreed_knowledge_base_part_operator_return;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_init3 (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _param;;

		_=> nrel_then:: .._agent_of_forming_agreed_knowledge_base_part_operator_init4;;
		_=> nrel_else:: .._agent_of_forming_agreed_knowledge_base_part_operator_return;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_return (*
		<-_ return;;

	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_init4 (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: concerted_part_of_kb;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _concerted_part;;

		_=> nrel_then:: .._agent_of_forming_agreed_knowledge_base_part_operator_init4_1;;
		_=> nrel_else:: .._agent_of_forming_agreed_knowledge_base_part_operator_unsuccess;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_init4_1 (*
		<-_ ifCoin;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: concerted_part_of_kb;;

		_=> nrel_then:: .._agent_of_forming_agreed_knowledge_base_part_operator_find_substantive_part_of_kb;;
		_=> nrel_else:: .._agent_of_forming_agreed_knowledge_base_part_operator_return;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_unsuccess (*
		<-_ genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_finished_unsuccessfully;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action;;

		_=> nrel_goto:: .._agent_of_forming_agreed_knowledge_base_part_operator_return;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_find_substantive_part_of_kb (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: substantive_part_of_the_knowledge_base;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _substantive_part_of_kb;;

		_=> nrel_then:: .._agent_of_forming_agreed_knowledge_base_part_operator_section14;;
		_=> nrel_else:: .._agent_of_forming_agreed_knowledge_base_part_operator_find_documentation_section;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_section14 (*
		<-_ call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_section_in_context;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_forming_agreed_knowledge_base_part_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _substantive_part_of_kb;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _concerted_part;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr_subst_part;;

		_=> nrel_goto:: .._agent_of_forming_agreed_knowledge_base_part_operator_section14_wait;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_find_documentation_section (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: documentation_of_the_computer_system;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _documentation_section;;

		_=> nrel_then:: .._agent_of_forming_agreed_knowledge_base_part_operator_section15;;
		_=> nrel_else:: .._agent_of_forming_agreed_knowledge_base_part_operator_find_context_of_subject_of_kb_of_the_global_kb;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_section15 (*
		<-_ call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_section_in_context;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_forming_agreed_knowledge_base_part_param1 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _documentation_section;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _concerted_part;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr_documentation;;

		_=> nrel_goto:: .._agent_of_forming_agreed_knowledge_base_part_operator_section15_wait;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_find_context_of_subject_of_kb_of_the_global_kb (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: context_of_subject_of_knowledge_base_of_the_Global_Knowledge_Base;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _context_of_subject_of_kb_of_the_global_kb;;

		_=> nrel_then:: .._agent_of_forming_agreed_knowledge_base_part_operator_section16;;
		_=> nrel_else:: .._agent_of_forming_agreed_knowledge_base_part_operator_success;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_section16 (*
		<-_ call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_section_in_context;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_forming_agreed_knowledge_base_part_param2 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _context_of_subject_of_kb_of_the_global_kb;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _concerted_part;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr_glob_context;;

		_=> nrel_goto:: .._agent_of_forming_agreed_knowledge_base_part_operator_section16_wait;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_success (*
		<-_ genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_finished_successfully;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action;;

		_=> nrel_goto:: .._agent_of_forming_agreed_knowledge_base_part_operator_return;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_section16_wait (*
		<-_ waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr_glob_context;;

		_=> nrel_goto:: .._agent_of_forming_agreed_knowledge_base_part_operator_success;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_section15_wait (*
		<-_ waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr_documentation;;

		_=> nrel_goto:: .._agent_of_forming_agreed_knowledge_base_part_operator_find_context_of_subject_of_kb_of_the_global_kb;;
	*);;

	_-> .._agent_of_forming_agreed_knowledge_base_part_operator_section14_wait (*
		<-_ waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr_subst_part;;

		_=> nrel_goto:: .._agent_of_forming_agreed_knowledge_base_part_operator_find_documentation_section;;
	*);;

*);;

*];;
