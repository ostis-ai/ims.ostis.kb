lib_component_proc_of_adding_struct_elems_to_context =  [*
proc_of_adding_struct_elems_to_context
=> nrel_main_idtf:
	[scp-программа добавления элементов структуры в контекст] (* <- lang_ru;; *);
	[scp-program of adding structures elements to context] (* <- lang_en;; *);
<- scp_program;
-> rrel_key_sc_element: .._process1;;

proc_of_adding_struct_elems_to_context = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _struct;
_-> rrel_2:: rrel_in:: _context;
_-> rrel_3:: rrel_in:: _visited_inner_structs;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._proc_of_adding_struct_elems_to_context_operator_A0 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _visited_inner_structs;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _struct;;

		_=> nrel_goto:: .._proc_of_adding_struct_elems_to_context_operator_A1;;
	*);;

	_-> .._proc_of_adding_struct_elems_to_context_operator_A1 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _struct;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _any_element;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_2:: rrel_set_3:: _context;;

		_=> nrel_goto:: .._proc_of_adding_struct_elems_to_context_operator_A2;;
	*);;

	_-> .._proc_of_adding_struct_elems_to_context_operator_A2 (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _struct;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _any_element;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_assign:: rrel_scp_var:: _role_relation;;
		_-> rrel_fixed:: rrel_scp_const:: rrel_set_4:: rrel_set_5:: _context;;

		_=> nrel_goto:: .._proc_of_adding_struct_elems_to_context_operator_A3;;
	*);;

	_-> .._proc_of_adding_struct_elems_to_context_operator_A3 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _struct;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: rrel_struct:: _inner_struct;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_3:: _inner_structs;;

		_=> nrel_then:: .._proc_of_adding_struct_elems_to_context_operator_A4_loop_enter;;
		_=> nrel_else:: .._proc_of_adding_struct_elems_to_context_operator_return;;
	*);;

	_-> .._proc_of_adding_struct_elems_to_context_operator_A4_loop_enter (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _inner_structs;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _inner_struct;;

		_=> nrel_then:: .._proc_of_adding_struct_elems_to_context_operator_A5;;
		_=> nrel_else:: .._proc_of_adding_struct_elems_to_context_operator_A8_erase_temp_set;;
	*);;

	_-> .._proc_of_adding_struct_elems_to_context_operator_return (*
		_<- return;;

	*);;

	_-> .._proc_of_adding_struct_elems_to_context_operator_A5 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._proc_of_adding_struct_elems_to_context_operator_A6_0;;
	*);;

	_-> .._proc_of_adding_struct_elems_to_context_operator_A8_erase_temp_set (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _inner_structs;;

		_=> nrel_goto:: .._proc_of_adding_struct_elems_to_context_operator_return;;
	*);;

	_-> .._proc_of_adding_struct_elems_to_context_operator_A6_0 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _visited_inner_structs;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _inner_struct;;

		_=> nrel_then:: .._proc_of_adding_struct_elems_to_context_operator_A4_loop_enter;;
		_=> nrel_else:: .._proc_of_adding_struct_elems_to_context_operator_A6;;
	*);;

	_-> .._proc_of_adding_struct_elems_to_context_operator_A6 (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_struct_elems_to_context;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: proc_of_adding_struct_elems_to_context_operator_A6_params (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _inner_struct;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _context;;
			_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _visited_inner_structs;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._proc_of_adding_struct_elems_to_context_operator_A7;;
	*);;

	_-> .._proc_of_adding_struct_elems_to_context_operator_A7 (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._proc_of_adding_struct_elems_to_context_operator_A4_loop_enter;;
	*);;

*);;

*];;
*];;
