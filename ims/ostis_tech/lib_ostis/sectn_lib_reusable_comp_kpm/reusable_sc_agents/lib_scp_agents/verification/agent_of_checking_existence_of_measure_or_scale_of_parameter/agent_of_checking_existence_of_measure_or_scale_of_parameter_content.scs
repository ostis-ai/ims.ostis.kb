agent_of_checking_existence_of_measure_or_scale_of_parameter
=> nrel_main_idtf:
	[агентная scp-программа проверки указания шкалы или единицы измерения для значений параметров в рамках заданной структуры] (* <- lang_ru;; *);
	[agent scp-program of checking indication of scale or measure for values of parameters within given structure] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_checking_existence_of_measure_or_scale_of_parameter = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator1A (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator1B;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator1B (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_check_indication_of_scale_or_measure_for_values_of_parameters_within_given_structure;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator1C;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_return;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator1C (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: rrel_struct:: _param;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2C;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_call_proc_of_adding_validating_message;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_return (*
		_<- return;;

	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2C (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _answer_of_checking_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2D;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_call_proc_of_adding_validating_message (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_validating_message;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [Wrong parameter!It must be structure];;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr3;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_waitreturn_proc_of_adding_validating_message;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_waitreturn_proc_of_adding_validating_message (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr3;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_return;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2D (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: _set_of_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3A;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3A (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_node:: rrel_scp_var:: _node1;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_3:: _set_of_nodes_in_struct;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3B;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3B (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _set_of_nodes_in_struct;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _concrete_parameter;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3C;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator4A;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3C (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3D;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator4A (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: rrel_struct:: _answer_of_proc_of_checking_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5A;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5A (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: _set_of_measures_and_scale;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5B;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5B (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _set_of_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _characteristic_of_parameter;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5C;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7G;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5C (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5D;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7G (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _set_of_measures_and_scale;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_gen_agent_work_result;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_gen_agent_work_result (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_generation_of_verification_answer;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_param1 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: action_to_check_indication_of_scale_or_measure_for_values_of_parameters_within_given_structure;;
			_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
			_-> rrel_4:: rrel_fixed:: rrel_scp_const:: structure_of_checking_existence_of_measure_or_scale_of_parameter;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_waitreturn_generation_agent_work_result;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_waitreturn_generation_agent_work_result (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator8A;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator8A (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_finished_successfully;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_return;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5D (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _characteristic_of_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _some_node;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_3:: _set_of_measures_and_scale;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6A;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5B;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6A (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _set_of_measures_and_scale;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _value_of_parameter;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6B;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5B;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6B (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6C;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6C (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _value_of_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _some_value;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_assign:: rrel_scp_var:: _some_relation;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6D;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6A;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6D (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: measurement_of_fixed_measure;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_relation;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6F;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6E;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6F (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _some_relation;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _some_measure;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_measure;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6G;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_9;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6E (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: scale;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_relation;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6H;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6A;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6H (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _some_relation;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _some_measure;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_zero_mark;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6K;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_9;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6K (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _characteristic_of_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_measure;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6A;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_3;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_9 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: measurement_of_fixed_measure;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_relation;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_10;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_12;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_10 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: measurement_of_fixed_measure;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_14;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_11;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_12 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: scale;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_15;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_13;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_15 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: scale;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_relation;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: _answer_of_proc_of_checking_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_16;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_13 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: scale;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_15;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_16 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_relation;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7B;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7B (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _value_of_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _some_value;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_var:: _some_relation;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_3:: rrel_set_4:: _answer_of_proc_of_checking_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7C;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7C (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _number_or_value;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_value;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_1:: rrel_set_2:: _answer_of_proc_of_checking_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7D;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7D (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _value_of_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7E;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7E (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _characteristic_of_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _value_of_parameter;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: _answer_of_proc_of_checking_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7F;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7F (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _characteristic_of_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6A;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_14 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: measurement_of_fixed_measure;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_relation;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: _answer_of_proc_of_checking_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_16;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_11 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: measurement_of_fixed_measure;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_14;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_3 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_zero_mark;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_5;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_4;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_5 (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _some_relation;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_measure;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_zero_mark;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_4:: _answer_of_proc_of_checking_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_7;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_4 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_zero_mark;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_5;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_7 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_measure;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_9;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_8;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_8 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_measure;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_9;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6G (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _characteristic_of_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_measure;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6A;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_1;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_1 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_measure;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_6;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_2;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_6 (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _some_relation;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_measure;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_measure;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_4:: _answer_of_proc_of_checking_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_7;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_2 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_proc_of_checking_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_measure;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A_6;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3D (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _concrete_parameter;;

		_=> nrel_then:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3E;;
		_=> nrel_else:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3B;;
	*);;

	_-> .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3E (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _set_of_parameter;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _concrete_parameter;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3B;;
	*);;

*);;

*];;
