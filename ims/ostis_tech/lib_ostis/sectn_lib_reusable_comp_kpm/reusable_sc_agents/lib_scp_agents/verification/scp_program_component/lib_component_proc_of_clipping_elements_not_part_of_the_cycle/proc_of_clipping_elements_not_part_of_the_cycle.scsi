scp_program-> proc_of_clipping_elements_not_part_of_the_cycle
(*
     -> rrel_params: .proc_of_clipping_elements_not_part_of_the_cycle_params
      	(*
		-> rrel_1: rrel_in: _start_element;;
		-> rrel_2: rrel_in: _last_element;;
		-> rrel_3: rrel_in: _struct_node;;
		-> rrel_4: rrel_in: _order_relation_param;;
	 	 *);;

	 -> rrel_operators: .proc_of_clipping_elements_not_part_of_the_cycle_set
	 	(*
	 	-> rrel_init: .proc_of_clipping_elements_not_part_of_the_cycle_operator1A
	 		(*
	 		<- searchSetStr5;;

	 		-> rrel_1: rrel_fixed: rrel_scp_var: _start_element;;
	 		-> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc1;;
	 		-> rrel_3: rrel_assign: rrel_scp_var: _node1;;
	 		-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
	 		-> rrel_5: rrel_fixed: rrel_scp_var: _order_relation_param;;

	 		-> rrel_set_3: rrel_assign: rrel_scp_var: _set_for_next_node;;

	 		=> nrel_then: .proc_of_clipping_elements_not_part_of_the_cycle_operator1B;;
	 		=> nrel_else: .proc_of_clipping_elements_not_part_of_the_cycle_operator_return;;
	 		*);;

	 	-> .proc_of_clipping_elements_not_part_of_the_cycle_operator1B
	 		(*
	 		<- searchElStr3;;

	 		-> rrel_1: rrel_fixed: rrel_scp_var: _set_for_next_node;;
	 		-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
	 		-> rrel_3: rrel_assign: rrel_scp_var: _node1;;

	 		=> nrel_then: .proc_of_clipping_elements_not_part_of_the_cycle_operator1C;;
	 		=> nrel_else: .proc_of_clipping_elements_not_part_of_the_cycle_operator3A;;
	 		*);;

	 	-> .proc_of_clipping_elements_not_part_of_the_cycle_operator1C
	 		(*
	 		<- eraseEl;;

	 		-> rrel_1: rrel_erase: rrel_fixed: rrel_scp_var: rrel_pos_const_perm: _arc1;;

	 		=> nrel_goto: .proc_of_clipping_elements_not_part_of_the_cycle_operator1D;;
	 		*);;

	 	-> .proc_of_clipping_elements_not_part_of_the_cycle_operator1D
	 		(*
	 		<- searchElStr3;;

	 		-> rrel_1: rrel_fixed: rrel_scp_var: _struct_node;;
	 		-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
	 		-> rrel_3: rrel_fixed: rrel_scp_var: _node1;;
	 
	 		=> nrel_then: .proc_of_clipping_elements_not_part_of_the_cycle_operator1F;;
	 		=> nrel_else: .proc_of_clipping_elements_not_part_of_the_cycle_operator1B;;
	 		*);;

	 	-> .proc_of_clipping_elements_not_part_of_the_cycle_operator1F
	 		(*
	 		<- searchElStr5;;

	 		-> rrel_1: rrel_fixed: rrel_scp_var: _start_element;;
	 		-> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc1;;
	 		-> rrel_3: rrel_fixed: rrel_scp_var: _node1;;
	 		-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
	 		-> rrel_5: rrel_fixed: rrel_scp_var: _order_relation_param;;

	 		=> nrel_then: .proc_of_clipping_elements_not_part_of_the_cycle_operator1G;;
	 		=> nrel_else: .proc_of_clipping_elements_not_part_of_the_cycle_operator1B;;
	 		*);;

	 	-> .proc_of_clipping_elements_not_part_of_the_cycle_operator1G
	 		(*
	 		<- searchElStr3;;

	 		-> rrel_1: rrel_fixed: rrel_scp_var: _struct_node;;
	 		-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
	 		-> rrel_3: rrel_fixed: rrel_scp_var: rrel_pos_const_perm: _arc2;;

	 		=> nrel_then: .proc_of_clipping_elements_not_part_of_the_cycle_operator1H;;
	 		=> nrel_else: .proc_of_clipping_elements_not_part_of_the_cycle_operator1I;;
	 		*);;

	 	-> .proc_of_clipping_elements_not_part_of_the_cycle_operator1H
	 		(*
	 		<- eraseEl;;

	 		-> rrel_1: rrel_erase: rrel_fixed: rrel_scp_var: rrel_pos_const_perm: _arc;;

	 		=> nrel_goto: .proc_of_clipping_elements_not_part_of_the_cycle_operator1I;;
	 		*);;

	 	-> .proc_of_clipping_elements_not_part_of_the_cycle_operator1I
	 		(*
	 		<- searchElStr3;;

	 		-> rrel_1: rrel_fixed: rrel_scp_var: _struct_node;;
	 		-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
	 		-> rrel_3: rrel_fixed: rrel_scp_var: rrel_common: _arc1;;

	 		=> nrel_then: .proc_of_clipping_elements_not_part_of_the_cycle_operator1J;;
	 		=> nrel_else: .proc_of_clipping_elements_not_part_of_the_cycle_operator2A;;
	 		*);;

	 	-> .proc_of_clipping_elements_not_part_of_the_cycle_operator1J
	 		(*
	 		<- eraseEl;;

	 		-> rrel_1: rrel_erase: rrel_fixed: rrel_scp_var: rrel_pos_const_perm: _arc;;

	 		=> nrel_goto: .proc_of_clipping_elements_not_part_of_the_cycle_operator2A;;
	 		*);;

	 	-> .proc_of_clipping_elements_not_part_of_the_cycle_operator2A
	 		(*
	 		<- ifCoin;;

	 		-> rrel_1: rrel_fixed: rrel_scp_var: _last_element;;
	 		-> rrel_2: rrel_fixed: rrel_scp_var: _node1;;

	 		=> nrel_then: .proc_of_clipping_elements_not_part_of_the_cycle_operator3A;;
	 		=> nrel_else: .proc_of_clipping_elements_not_part_of_the_cycle_operator2B;;
	 		*);;


	 	-> .proc_of_clipping_elements_not_part_of_the_cycle_operator2B
			(*
			<- call;;
			-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_clipping_elements_not_part_of_the_cycle;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .proc_of_clipping_elements_not_part_of_the_cycle_operator2B_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _node1;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _last_element;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _struct_node;;
				-> rrel_4: rrel_fixed: rrel_scp_var: _order_relation_param;;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .proc_of_clipping_elements_not_part_of_the_cycle_operator2C;;

			*);;

		-> .proc_of_clipping_elements_not_part_of_the_cycle_operator2C
			(*
			<- waitReturn;;
		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .proc_of_clipping_elements_not_part_of_the_cycle_operator1B;;
			*);;
		 	
		-> .proc_of_clipping_elements_not_part_of_the_cycle_operator3A
	 		(*
	 		<- searchElStr3;;

	 		-> rrel_1: rrel_fixed: rrel_scp_var: _struct_node;;
	 		-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
	 		-> rrel_3: rrel_fixed: rrel_scp_var: _start_element;;
	 
	 		=> nrel_goto: .proc_of_clipping_elements_not_part_of_the_cycle_operator3B;;
	 		*);;

		-> .proc_of_clipping_elements_not_part_of_the_cycle_operator3B
	 		(*
	 		<- eraseEl;;

	 		-> rrel_1: rrel_erase: rrel_fixed: rrel_scp_var: rrel_pos_const_perm: _arc1;;

	 		=> nrel_goto: .proc_of_clipping_elements_not_part_of_the_cycle_operator_return;;
	 		*);;
	
		-> .proc_of_clipping_elements_not_part_of_the_cycle_operator_return
			(*
			<- return;;
			*);;	
				
	 	*);;

	 *);;
