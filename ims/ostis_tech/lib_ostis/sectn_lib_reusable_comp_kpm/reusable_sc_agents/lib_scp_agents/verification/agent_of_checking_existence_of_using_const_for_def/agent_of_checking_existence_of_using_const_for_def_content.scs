agent_of_checking_existence_of_using_const_for_def
=> nrel_main_idtf:
	[агентная scp-программа проверки указания используемых констант для определений понятий, имеющихся в рамках заданной структуры] (* <- lang_ru;; *);
	[agent scp-program of checking existence using constants constants for concept definition within the given structure] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_checking_existence_of_using_const_for_def = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_checking_existence_of_using_const_for_def_operator1A (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _action;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1B;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1B (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_check_existence_of_using_const_for_def;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1D;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator_return;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1D (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: rrel_struct:: _param;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1D_find_context;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator_invalid_first_param_msg;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_return (*
		_<- return;;

	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1D_find_context (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _common_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _context;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc_in_relation;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_context_of_action;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1D_to_true;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator1D_to_false;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_invalid_first_param_msg (*
		_<- varAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _validation_message;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [Wrong 1st parameter! It must be a structure.];;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator_call_proc_of_adding_validating_message;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_call_proc_of_adding_validating_message (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_validating_message;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_checking_existence_of_using_const_for_def_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _validation_message;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator_waitreturn_proc_of_adding_validating_message;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_waitreturn_proc_of_adding_validating_message (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator_return;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1D_to_true (*
		_<- varAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator_validate_context;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1D_to_false (*
		_<- varAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [false];;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1E_gen_answer_node;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1E_gen_answer_node (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: rrel_struct:: _incorrect_structure;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1E_gen_concepts_node;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1E_gen_concepts_node (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _concepts;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1F_get_params_1;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1F_get_params_1 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _concept;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_3:: _concepts;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1F_get_params_2;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1F_get_params_2 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _concept;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_3:: _concepts;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1F_get_params_3;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1F_get_params_3 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _concept;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_3:: _concepts;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_enter;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_enter (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _concepts;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _concept;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop1;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator_gen_verification_answer;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop1 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop2;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_gen_verification_answer (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_generation_of_verification_answer;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_checking_existence_of_using_const_for_def_param1 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: action_to_check_existence_of_using_const_for_def;;
			_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _incorrect_structure;;
			_-> rrel_4:: rrel_fixed:: rrel_scp_const:: structure_which_contains_concepts_with_definition_but_without_using_constants;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr3;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator_waitreturn_generation_verification_answer;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_waitreturn_generation_verification_answer (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr3;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator_temp_set_2_is_not_null;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_temp_set_2_is_not_null (*
		_<- ifVarAssign;;
		_-> rrel_1:: rrel_scp_var:: _concepts;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator_erase_temp_set_2;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator_temp_set_3_is_not_null;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_erase_temp_set_2 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _concepts;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator_temp_set_3_is_not_null;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_temp_set_3_is_not_null (*
		_<- ifVarAssign;;
		_-> rrel_1:: rrel_scp_var:: _def_set;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator_erase_temp_set_3;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator_return;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_erase_temp_set_3 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _def_set;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator_return;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop2 (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _node;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _concept;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_key_sc_element;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_1:: _all_def_set;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic1;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_enter;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic1 (*
		_<- stringIfEq;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1H_context_logic1;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop3;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1H_context_logic1 (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: _def_set;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1H_context_logic2;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop3 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _all_def_set;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _some_element;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_3:: _def_set;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop3_delete;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop3_delete (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _all_def_set;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator_def_search_loop_enter;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_def_search_loop_enter (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _def_set;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _def;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator_def_search_loop1;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_enter;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_def_search_loop1 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_is_def;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_is_def (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: definition;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _def;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic4;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_is_explanation;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic4 (*
		_<- stringIfEq;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic5;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_check_using_constants;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_is_explanation (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: explanation;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _def;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic4;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator_def_search_loop_enter;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic5 (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _node;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _def;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_using_constants;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_1:: _using_constants;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic6;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_add_incorrect_concept;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_check_using_constants (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _node;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _def;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_using_constants;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator_def_search_loop_enter;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_add_incorrect_concept;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_add_incorrect_concept (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _incorrect_structure;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _concept;;
		_-> rrel_4:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_key_sc_element;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_enter;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic6 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _using_constants;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _some_element;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic7;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_loop_add_incorrect_concept;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic7 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic8;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic8 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _context;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_element;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator_def_search_loop_enter;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic6;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1H_context_logic2 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _all_def_set;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _some_def;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1H_context_logic3;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic3_delete;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1H_context_logic3 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1H_context_logic4;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1G_context_logic3_delete (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _all_def_set;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator_def_search_loop_enter;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1H_context_logic4 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _context;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_def;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1H_context_logic5;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator1H_context_logic2;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator1H_context_logic5 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _def_set;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_def;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator1H_context_logic2;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_validate_context (*
		_<- ifType;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: rrel_struct:: _context;;

		_=> nrel_then:: .._agent_of_checking_existence_of_using_const_for_def_operator1E_gen_answer_node;;
		_=> nrel_else:: .._agent_of_checking_existence_of_using_const_for_def_operator_invalid_second_param_msg;;
	*);;

	_-> .._agent_of_checking_existence_of_using_const_for_def_operator_invalid_second_param_msg (*
		_<- varAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _validation_message;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [Wrong 2nd parameter! It must be a structure.];;

		_=> nrel_goto:: .._agent_of_checking_existence_of_using_const_for_def_operator_call_proc_of_adding_validating_message;;
	*);;

*);;

*];;
