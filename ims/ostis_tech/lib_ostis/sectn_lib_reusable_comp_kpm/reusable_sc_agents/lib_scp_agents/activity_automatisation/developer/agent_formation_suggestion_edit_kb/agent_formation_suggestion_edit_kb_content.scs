agent_formation_suggestion_edit_kb
=> nrel_main_idtf:
	[агентная scp-программа формирования предложения по редактированию базы знаний] (* <- lang_ru;; *);
	[agent scp-program of the formation of proposals for editing the knowledge base] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_formation_suggestion_edit_kb = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_formation_suggestion_edit_kb_operator_A1 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_A2;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_A2 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_form_a_proposal_for_editing_the_knowledge_base;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_find_user;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_return;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_find_user (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _user;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_authors;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_is_two_argument_command;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_return;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_return (*
		_<- return;;

	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_is_two_argument_command (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _param;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_1;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_validate_first_param;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_A3;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_validate_first_param (*
		_<- ifType;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: rrel_struct:: _param;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_get_section;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_call_proc_of_adding_validating_message;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_A3 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: rrel_struct:: _param;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_is_two_arg_command_assign_false;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_call_proc_of_adding_validating_message;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_is_two_arg_command_assign_false (*
		_<- contAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _is_two_arg_command;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [false];;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_check_proposer_relation;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_call_proc_of_adding_validating_message (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_validating_message;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_formation_suggestion_edit_kb_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [Wrong first parameter! It must be a structure(rrel_struct)];;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_waitreturn_proc_of_adding_validating_message;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_waitreturn_proc_of_adding_validating_message (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_return;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_check_proposer_relation (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _user;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_authors;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_B1;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_gen_proposer_relation;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_B1 (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _action;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_B2;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_gen_proposer_relation (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _user;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_authors;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_B1;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_B2 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_consider_proposal_for_editing_knowledge_base;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_B3;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_B3 (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_proposal;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_find_action_by_target_relation;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_find_action_by_target_relation (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _action_todo;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_result;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_find_action_todo_question_initiated_arc;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_is_two_arg_command;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_find_action_todo_question_initiated_arc (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_initiated;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc_to_remove;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action_todo;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator1E1_erase_question_initianted_arc;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_E2;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_is_two_arg_command (*
		_<- stringIfEq;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _is_two_arg_command;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_add_object_to_action;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_B4;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_add_object_to_action (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _section_param;;
		_-> rrel_4:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_object;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_B4;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_B4 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_initiated;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_find_draft_arc;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_find_draft_arc (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: draft;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _draft_arc_to_remove;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_erase_draft_arc;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_call_adding;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_erase_draft_arc (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _draft_arc_to_remove;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_call_adding;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_call_adding (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_action_sign_to_current_processes;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_formation_suggestion_edit_kb_param1 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_waitreturn_adding;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_waitreturn_adding (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator1E1_gen_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator1E1_gen_answer (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _answer_formation_suggestion_edit_kb;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_scn_add_new_act_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_scn_add_new_act_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_formation_suggestion_edit_kb;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: action_to_consider_proposal_for_editing_knowledge_base;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_scn_add_new_action_arc_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_scn_add_new_action_arc_to_answer (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_consider_proposal_for_editing_knowledge_base;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: _answer_formation_suggestion_edit_kb;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_scn_add_suggestion_relation_arcs_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_scn_add_suggestion_relation_arcs_to_answer (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_proposal;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_4:: _answer_formation_suggestion_edit_kb;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_scn_add_quest2_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_scn_add_quest2_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_formation_suggestion_edit_kb;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_scn_add_target_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_scn_add_target_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_formation_suggestion_edit_kb;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_scn_add_suggestion_relation_node_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_scn_add_suggestion_relation_node_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_formation_suggestion_edit_kb;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_proposal;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_add_result_proposer_arcs_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_add_result_proposer_arcs_to_answer (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _name;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_authors;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_3:: rrel_set_4:: _answer_formation_suggestion_edit_kb;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_scn_add_proposer_relation_node_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_scn_add_proposer_relation_node_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_formation_suggestion_edit_kb;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_authors;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_add_param_struct_content_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_add_param_struct_content_to_answer (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _content;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_3:: _answer_formation_suggestion_edit_kb;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator2F_find_inner_struct_node_to_answer_formation_suggestion_edit_kb;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator2F_find_inner_struct_node_to_answer_formation_suggestion_edit_kb (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: rrel_struct:: _inner_struct;;

		_=> nrel_then:: .._agent_oformation_suggestion_edit_kb_operator2F_add_inner_struct_content_to_answer_formation_suggestion_edit_kb;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_add_sequence_of_actions_relation_arcs_to_answer;;
	*);;

	_-> .._agent_oformation_suggestion_edit_kb_operator2F_add_inner_struct_content_to_answer_formation_suggestion_edit_kb (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _inner_struct;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _content;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_3:: _answer_formation_suggestion_edit_kb;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_add_sequence_of_actions_relation_arcs_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_add_sequence_of_actions_relation_arcs_to_answer (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _action_todo;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_sequence_of_actions;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_1:: rrel_set_2:: rrel_set_4:: _answer_formation_suggestion_edit_kb;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_scn_add_sequence_action_relation_to_answer;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_is_two_arg_command_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_scn_add_sequence_action_relation_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_formation_suggestion_edit_kb;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_sequence_of_actions;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_add_sequence_of_performer_relation_arcs_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_is_two_arg_command_to_answer (*
		_<- stringIfEq;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _is_two_arg_command;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_add_object_relation_arcs_to_answer;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_gen_full_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_add_object_relation_arcs_to_answer (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _section;;
		_-> rrel_4:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_object;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_3:: rrel_set_4:: _answer_formation_suggestion_edit_kb;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_scn_add_object_relation_node_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_gen_full_answer (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _answer_formation_suggestion_edit_kb;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_answer;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_E1;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_E1 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_finished_successfully;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_return;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_scn_add_object_relation_node_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_formation_suggestion_edit_kb;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_object;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_gen_full_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_add_sequence_of_performer_relation_arcs_to_answer (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action_todo;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _performer;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_performer;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_3:: rrel_set_4:: _answer_formation_suggestion_edit_kb;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_scn_add_performer_relation_node_to_answer;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_is_two_arg_command_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_scn_add_performer_relation_node_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_formation_suggestion_edit_kb;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_performer;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_is_two_arg_command_to_answer;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator1E1_erase_question_initianted_arc (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc_to_remove;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_mark_action_todo_as_past_entity;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_E2 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_finished_unsuccessfully;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_return;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_mark_action_todo_as_past_entity (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: last_entity;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action_todo;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_gen_action_todo_as_past_entity;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_gen_action_todo_as_past_entity (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action_todo;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action;;
		_-> rrel_4:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_sequence_of_actions;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_is_two_arg_command;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_get_section (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _section_param;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_2;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_validate_second_param_1;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_validate_second_param_1 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: atomic_section;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _section_param;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_is_two_arg_command_assign_true;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_validate_second_param_2;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_is_two_arg_command_assign_true (*
		_<- contAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _is_two_arg_command;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_check_proposer_relation;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_validate_second_param_2 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: non_atomic_section;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _section_param;;

		_=> nrel_then:: .._agent_formation_suggestion_edit_kb_operator_is_two_arg_command_assign_true;;
		_=> nrel_else:: .._agent_formation_suggestion_edit_kb_operator_call_proc_of_adding_validating_message_second_param;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_call_proc_of_adding_validating_message_second_param (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_validating_message;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_formation_suggestion_edit_kb_param2 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [Wrong second parameter! It must be a section];;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_waitreturn_proc_of_adding_validating_message_second_param;;
	*);;

	_-> .._agent_formation_suggestion_edit_kb_operator_waitreturn_proc_of_adding_validating_message_second_param (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_formation_suggestion_edit_kb_operator_return;;
	*);;

*);;

*];;
