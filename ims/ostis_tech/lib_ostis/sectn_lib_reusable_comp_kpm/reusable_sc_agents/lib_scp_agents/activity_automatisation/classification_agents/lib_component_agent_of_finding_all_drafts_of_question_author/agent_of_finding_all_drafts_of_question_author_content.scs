agent_of_finding_all_drafts_of_action_author
=> nrel_main_idtf:
	[агентная scp-программа поиска всех черновиков автора вопроса] (* <- lang_ru;; *);
	[agent scp-program of finding all drafts of action author] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_finding_all_drafts_of_action_author = [*
.._process1
<-_ scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
<=_ nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_finding_all_drafts_of_action_author_operator_get_action (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _action;;

		_=> nrel_goto:: .._agent_of_finding_all_drafts_of_action_author_operator_check_action;;
	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_check_action (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_of_finding_all_drafts_of_action_author;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action;;

		_=> nrel_then:: .._agent_of_finding_all_drafts_of_action_author_operator_get_action_author;;
		_=> nrel_else:: .._agent_of_finding_all_drafts_of_action_author_operator_return;;
	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_get_action_author (*
		<-_ searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action;;
		_-> rrel_2:: rrel_assign:: rrel_common_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _user;;
		_-> rrel_4:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_authors;;

		_=> nrel_then:: .._agent_of_finding_all_drafts_of_action_author_operator_gen_new_action_1;;
		_=> nrel_else:: .._agent_of_finding_all_drafts_of_action_author_operator_return;;
	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_return (*
		<-_ return;;

	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_gen_new_action_1 (*
		<-_ genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _new_quest;;

		_=> nrel_goto:: .._agent_of_finding_all_drafts_of_action_author_operator_gen_new_action_2;;
	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_gen_new_action_2 (*
		<-_ genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_of_finding_all_drafts_of_author;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _new_quest;;

		_=> nrel_goto:: .._agent_of_finding_all_drafts_of_action_author_operator_gen_new_action_3;;
	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_gen_new_action_3 (*
		<-_ genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _new_quest;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _user;;

		_=> nrel_goto:: .._agent_of_finding_all_drafts_of_action_author_operator_gen_new_action_4;;
	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_gen_new_action_4 (*
		<-_ genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_initiated;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _new_quest;;

		_=> nrel_goto:: .._agent_of_finding_all_drafts_of_action_author_operator_check_answer_1;;
	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_check_answer_1 (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_finished_unsuccessfully;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc_result;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _new_quest;;

		_=> nrel_then:: .._agent_of_finding_all_drafts_of_action_author_operator_search_ready_answer;;
		_=> nrel_else:: .._agent_of_finding_all_drafts_of_action_author_operator_check_answer_2;;
	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_search_ready_answer (*
		<-_ searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _new_quest;;
		_-> rrel_2:: rrel_assign:: rrel_common_arc:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _agent_answer;;
		_-> rrel_4:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_result;;

		_=> nrel_goto:: .._agent_of_finding_all_drafts_of_action_author_operator_make_answer;;
	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_check_answer_2 (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_finished_successfully;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc_result;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _new_quest;;

		_=> nrel_then:: .._agent_of_finding_all_drafts_of_action_author_operator_search_ready_answer;;
		_=> nrel_else:: .._agent_of_finding_all_drafts_of_action_author_operator_check_answer_3;;
	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_check_answer_3 (*
		<-_ sys_wait;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: sc_event_before_erase_incoming_arc;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _new_quest;;

		_=> nrel_goto:: .._agent_of_finding_all_drafts_of_action_author_operator_check_answer_1;;
	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_make_answer (*
		<-_ genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action;;
		_-> rrel_2:: rrel_assign:: rrel_common_arc:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _agent_answer;;
		_-> rrel_4:: rrel_assign:: rrel_const:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_result;;

		_=> nrel_goto:: .._agent_of_finding_all_drafts_of_action_author_operator_successfully_finished_action;;
	*);;

	_-> .._agent_of_finding_all_drafts_of_action_author_operator_successfully_finished_action (*
		<-_ genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_finished_successfully;;
		_-> rrel_2:: rrel_assign:: rrel_const_perm_pos_arc:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action;;

		_=> nrel_goto:: .._agent_of_finding_all_drafts_of_action_author_operator_return;;
	*);;

*);;

*];;
