agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions
=> nrel_main_idtf:
	[агентная scp-программа поиска инициированных действий, относящихся к определённому классу действий] (* <- lang_ru;; *);
	[agent scp-program of finding initiated signs of actions related to specific class of actions] (* <- lang_en;; *);
	<- agent_scp_program;;

scp_program -> agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions 
	(*
	-> rrel_params: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_params 
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	-> rrel_operators: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator_set 
		(*
		-> rrel_init: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator1A 
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator1B;;
			*);;

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator1B 
			(*
		  	<- searchElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: action_of_finding_initiated_signs_of_actions_for_specific_class_of_actions;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

		  	=> nrel_then: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator1C;;
		  	=> nrel_else: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator_return;;
			*);;

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator1C 
			(*
		  	<- searchElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _class_of_action;;

		  	=> nrel_then: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator1D;;
		  	=> nrel_else: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator_return;;
			*);;

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator1D
            		(*
		 	<- ifType;;

		 	-> rrel_1: rrel_fixed:  sc_node_not_relation: rrel_scp_var: _class_of_action;;

		  	=> nrel_then: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator1E;;
		  	=> nrel_else: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_call_proc_of_adding_validating_message;;
		 	*);;  

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator1E
            		(*
		 	<- genEl;;

		 	-> rrel_1: rrel_assign: rrel_node: rrel_scp_var: rrel_const: _answer_agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions;;

		 	=> nrel_goto: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator1F;;
		 	*);;  

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator1F
            		(*
		 	<- genEl;;

		 	-> rrel_1: rrel_assign: rrel_node: rrel_scp_var: rrel_const: _set_of_initiated_actions;;

		 	=> nrel_goto: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2A;;
		 	*);;  

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2A
			(*
		  	<- searchSetStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _class_of_action;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _sign_of_action;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _set_of_signs_of_action;;

		  	=> nrel_then: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2B;;
		  	=> nrel_else: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator_return;;
			*);;

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2B
			(*
		  	<- searchElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_signs_of_action;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _some_sign;;

		  	=> nrel_then: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2C;;
		  	=> nrel_else: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2F;;
			*);;

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2C 
            		(*
		 	<- eraseEl;;

		 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc1;;

		 	=> nrel_goto: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2D;;
		 	*);; 

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2D
			(*
		  	<- searchElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: question_initiated;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _some_sign;;

		  	=> nrel_then: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2E;;
		  	=> nrel_else: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2B;;
			*);;

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2E
			(*
		  	<- genElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_initiated_actions;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _some_sign;;

		  	=> nrel_goto: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2B;;
			*);;

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator2F
           		 (*
		 	<- eraseEl;;

		 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _set_of_signs_of_action;;

		 	=> nrel_goto: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator3A;;
		 	*);; 

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator3A
			(*
		  	<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_displaying_initiated_actions;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator3A_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_initiated_actions;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _answer_agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions;;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator3B;;
			*);;

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator3B
			(*
		  	<- waitReturn;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator4A;;
			*);;

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator4A
			(*
		  	<- genElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
		  	-> rrel_2: rrel_assign: rrel_common: rrel_const: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _answer_agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_const: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;

		  	=> nrel_goto: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator_return;;
			*);;                                            
			
		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_call_proc_of_adding_validating_message 
			(*
		  	<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_adding_validating_message;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_call_proc_of_adding_validating_message_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _question;;
				-> rrel_2: rrel_fixed: rrel_scp_const: [Wrong first parameter! It must be a class];;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_call_proc_of_adding_validating_message_waitreturn_proc_of_adding_validating_message;;
			*);;

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_call_proc_of_adding_validating_message_waitreturn_proc_of_adding_validating_message
			(*
		  	<- waitReturn;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator_return;;
			*);;

		-> .agent_of_finding_initiated_signs_of_actions_for_specific_class_of_actions_operator_return 
			(*
		  	<- return;;
			*);;
		*);;
	*);;

























