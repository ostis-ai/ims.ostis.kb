agent_of_finding_approved_proposals_by_user
=> nrel_main_idtf:
	[агентная scp-программа поиска всех утвержденных предложений конкретным пользователем] (* <- lang_ru;; *);
	[agent scp-program of finding approved proposals by user] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_finding_approved_proposals_by_user = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_finding_approved_proposals_by_user1A (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp_of_action;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user1B;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user1B (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_of_finding_approved_proposals_by_user;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user1C;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_return;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user1C (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _param;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_validation_check;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_return;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_return (*
		_<- return;;

	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_validation_check (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: ui_user;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_2A_find_context;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_invalid_first_param_msg;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_2A_find_context (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _context;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_context_of_action;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_2A_set_context_existence_to_true;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_2A_set_context_existence_to_false;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_invalid_first_param_msg (*
		_<- varAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _validation_message;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [Wrong first parameter! It must be user sign(element of ui_user)];;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_call_proc_of_adding_validating_message_wrong_second_param;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_call_proc_of_adding_validating_message_wrong_second_param (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_validating_message;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_finding_approved_proposals_by_user_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _validation_message;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_waitreturn_proc_of_adding_validating_message_wrong_second_param;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_waitreturn_proc_of_adding_validating_message_wrong_second_param (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_return;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_2A_set_context_existence_to_true (*
		_<- varAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_set_necessary;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_2A_set_context_existence_to_false (*
		_<- varAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [false];;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_set_necessary;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_set_necessary (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _is_const_nodes_necessary;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user1D;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user1D (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _set_of_object_approved;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user1E;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user1E (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _answer_for_finding_approved_proposals_by_user;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user1F;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user1F (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _action_node;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_approved;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_1:: _set_of_object_approved;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user1G;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_call_proc_of_adding_general_action_comment;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user1G (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _set_of_object_approved;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc_remove;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _action_node;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user2G;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_check_is_const_nodes_necessary;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_call_proc_of_adding_general_action_comment (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_general_action_comment;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_finding_approved_proposals_by_user_param1 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [The search hasn't given any results];;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_waitreturn_proc_of_adding_general_action_comment;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_waitreturn_proc_of_adding_general_action_comment (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_erase_set_1;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_erase_set_1 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _set_of_object_approved;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_erase_set_2;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_erase_set_2 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _is_const_nodes_necessary;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_return;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user2G (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc_remove;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_finding_struct;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_check_is_const_nodes_necessary (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _is_const_nodes_necessary;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _node;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_add_nrel_approved;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_check_is_answer_empty;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_add_nrel_approved (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_approved;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_add_nrel_proposal;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_check_is_answer_empty (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _node;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user11A;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_call_proc_of_adding_general_action_comment;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user11A (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_4:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_result;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_erase_set_1;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_add_nrel_proposal (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_proposal;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_add_nrel_authors;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_add_nrel_authors (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_authors;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_add_param;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_add_param (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user11A;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_finding_struct (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action_node;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _structure;;
		_-> rrel_4:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_proposal;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_add_action_finding_arc_node;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user1G;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_add_action_finding_arc_node (*
		_<- stringIfEq;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_context_logic_1;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_1;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_context_logic_1 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _context;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action_node;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_context_logic_3;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user1G;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_1 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _some_sign_class;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action_node;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_1:: _set_of_some_sign_class;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_2;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_2 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _set_of_some_sign_class;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc_remove_2;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _some_sign_class;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_3;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user3G;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_3 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc_remove_2;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_check_some_sign;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user3G (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action_node;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_4:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_approved;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_4:: _answer_for_finding_approved_proposals_by_user;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_add_action_node;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user1G;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_add_action_node (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action_node;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user4G;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user4G (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _action_node;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _structure;;
		_-> rrel_4:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_proposal;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_4:: _answer_for_finding_approved_proposals_by_user;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user6G;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user1G;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user6G (*
		_<- stringIfEq;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_context_logic_2;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_add_structure;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_context_logic_2 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _context;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _structure;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_add_structure;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user1G;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_add_structure (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _structure;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user9G;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user9G (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _structure;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _elem_of_struct;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_3:: _answer_for_finding_approved_proposals_by_user;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user10G;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user10G (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _structure;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _author;;
		_-> rrel_4:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_authors;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_3:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_4:: _answer_for_finding_approved_proposals_by_user;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_add_in_is_const_nodes_necessary;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user1G;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_add_in_is_const_nodes_necessary (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _is_const_nodes_necessary;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _author;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user1G;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_check_some_sign (*
		_<- stringIfEq;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_context_logic_0;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_4;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_context_logic_0 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _context;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_sign_class;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_4;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_2;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_4 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: section_developers_actions_knowledge_base_cooperation_hierarchy;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_sign_class;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_key_sc_element;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_5;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_2;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_5 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_for_finding_approved_proposals_by_user;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _some_sign_class;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_6;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_6 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _some_sign_class;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _action_node;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: _answer_for_finding_approved_proposals_by_user;;

		_=> nrel_goto:: .._agent_of_finding_approved_proposals_by_user3G;;
	*);;

	_-> .._agent_of_finding_approved_proposals_by_user_context_logic_3 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _context;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _structure;;

		_=> nrel_then:: .._agent_of_finding_approved_proposals_by_user_find_set_some_sign_class_1;;
		_=> nrel_else:: .._agent_of_finding_approved_proposals_by_user1G;;
	*);;

*);;

*];;
