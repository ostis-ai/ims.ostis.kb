agent_rejection_verification_result_of_offer
=> nrel_main_idtf:
	[агентная scp-программа для отклонения результата верификации предложения] (* <- lang_ru;; *);
	[agent scp-program of rejection of verification result of offer] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_rejection_verification_result_of_offer = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_rejection_verification_result_of_offer_operator1A (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator1B;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator1B (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_reject_the_result_of_proposal_verification;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_then:: .._agent_rejection_verification_result_of_offer_operator1D;;
		_=> nrel_else:: .._agent_rejection_verification_result_of_offer_operator_return;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator1D (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _act_param;;

		_=> nrel_then:: .._agent_rejection_verification_result_of_offer_operator1P;;
		_=> nrel_else:: .._agent_rejection_verification_result_of_offer_operator_return;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_return (*
		_<- return;;

	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator1P (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_initiated;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _act_param;;

		_=> nrel_then:: .._agent_rejection_verification_result_of_offer_operator_correct_act_checker;;
		_=> nrel_else:: .._agent_rejection_verification_result_of_offer_operator_call_proc_of_adding_validating_message;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_correct_act_checker (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_consider_result_of_proposal_verification;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _act_param;;

		_=> nrel_then:: .._agent_rejection_verification_result_of_offer_operator_find_user;;
		_=> nrel_else:: .._agent_rejection_verification_result_of_offer_operator_call_proc_of_adding_validating_message;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_call_proc_of_adding_validating_message (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_validating_message;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_rejection_verification_result_of_offer_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [Wrong first parameter! It must be sign of action to consider result of proposal verification(action_to_consider_result_of_proposal_verification)];;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_waitreturn_proc_of_adding_validating_message;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_waitreturn_proc_of_adding_validating_message (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_return;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_find_user (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _user;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_authors;;

		_=> nrel_then:: .._agent_rejection_verification_result_of_offer_operator_correct_user_checker;;
		_=> nrel_else:: .._agent_rejection_verification_result_of_offer_operator_return;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_correct_user_checker (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _uknown_entity;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _user;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_administrator;;

		_=> nrel_then:: .._agent_rejection_verification_result_of_offer_operator1_mark_act_param_as_past_entity;;
		_=> nrel_else:: .._agent_rejection_verification_result_of_offer_operator_return;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator1_mark_act_param_as_past_entity (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: last_entity;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _act_param;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator1_gen_rejection_relation_act;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator1_gen_rejection_relation_act (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _act_param;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _user;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_rejected;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_call_removing;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_call_removing (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_removing_action_sign_from_current_processes;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_rejection_verification_result_of_offer_param1 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _act_param;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_waitreturn_removing;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_waitreturn_removing (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_call_adding;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_call_adding (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_action_sign_to_history_of_the_development;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_rejection_verification_result_of_offer_param2 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _act_param;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_waitreturn_adding;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_waitreturn_adding (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_gen_answer_node;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_gen_answer_node (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _answer_rejection_verification_result_of_offer;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_add_previous_act_to_answer;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_add_previous_act_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_rejection_verification_result_of_offer;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _act_param;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_add_result_desicion_arcs_to_answer;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_add_result_desicion_arcs_to_answer (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _act_param;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _admin_name;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_rejected;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_3:: rrel_set_4:: _answer_rejection_verification_result_of_offer;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_add_rejected_relation_to_answer;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_add_rejected_relation_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_rejection_verification_result_of_offer;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_rejected;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_add_object_relation_arcs_to_answer;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_add_object_relation_arcs_to_answer (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _act_param;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _verification_structure;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_object;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_3:: rrel_set_4:: _answer_rejection_verification_result_of_offer;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_add_verification_structure_nodes_to_answer;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_add_verification_structure_nodes_to_answer (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _verification_structure;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _node;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_3:: _answer_rejection_verification_result_of_offer;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_add_object_relation_node_to_answer;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_add_object_relation_node_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_rejection_verification_result_of_offer;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_object;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_add_act_param_action_class_arc_to_answer_rejection_verification_result_of_offer;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_add_act_param_action_class_arc_to_answer_rejection_verification_result_of_offer (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_consider_result_of_proposal_verification;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _act_param;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: _answer_rejection_verification_result_of_offer;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_add_ct_param_action_class_to_answer_rejection_verification_result_of_offer;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_add_ct_param_action_class_to_answer_rejection_verification_result_of_offer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_rejection_verification_result_of_offer;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: action_to_consider_result_of_proposal_verification;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_gen_full_answer;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_gen_full_answer (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _answer_rejection_verification_result_of_offer;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_answer;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_pre_return;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_operator_pre_return (*
		_<- printEl;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_rejection_verification_result_of_offer;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_gen_question_finished_successfully;;
	*);;

	_-> .._agent_rejection_verification_result_of_offer_gen_question_finished_successfully (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_finished_successfully;;
		_-> rrel_2:: rrel_assign:: rrel_const:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_rejection_verification_result_of_offer_operator_return;;
	*);;

*);;

*];;
