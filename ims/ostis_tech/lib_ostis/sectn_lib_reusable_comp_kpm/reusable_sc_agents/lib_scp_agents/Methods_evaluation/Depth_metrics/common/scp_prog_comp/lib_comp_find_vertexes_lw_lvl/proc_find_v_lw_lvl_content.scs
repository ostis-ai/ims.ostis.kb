lib_component_proc_of_finding_vertexes_of_lower_level =  [*
proc_of_finding_vertexes_of_lower_level
<- scp_program;
-> rrel_key_sc_element: .._process1;;

proc_of_finding_vertexes_of_lower_level = [*
.._process1
<-_ scp_process;

_-> rrel_1:: rrel_in:: _current_node;
_-> rrel_2:: rrel_in:: _ontology;
_-> rrel_3:: rrel_in:: _vertexes_of_lower_level;
<=_ nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._proc_of_finding_vertexes_of_lower_level_operator_gen_uncheck_lower_level_vertexes (*
		<-_ genEl;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: _uncheck_lower_level_vertexes;;

		_=> nrel_goto:: .._proc_of_finding_vertexes_of_lower_level_operator_A1;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_A1 (*
		<-_ searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _current_node;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _node;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_3:: _uncheck_lower_level_vertexes;;

		_=> nrel_goto:: .._proc_of_finding_vertexes_of_lower_level_operator_get_all_taxonomy_relation;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_get_all_taxonomy_relation (*
		<-_ searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: taxonomy_relation;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _relation;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_3:: _all_taxonomy_relation;;

		_=> nrel_then:: .._proc_of_finding_vertexes_of_lower_level_operator_D1_loop_enter;;
		_=> nrel_else:: .._proc_of_finding_vertexes_of_lower_level_operator_G1_loop_enter;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_D1_loop_enter (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _all_taxonomy_relation;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _concrete_taxonomy_relation;;

		_=> nrel_then:: .._proc_of_finding_vertexes_of_lower_level_operator_D2_loop_arc_erase;;
		_=> nrel_else:: .._proc_of_finding_vertexes_of_lower_level_operator_G1_loop_enter;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_G1_loop_enter (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _uncheck_lower_level_vertexes;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _lower_level_vertex;;

		_=> nrel_then:: .._proc_of_finding_vertexes_of_lower_level_operator_G2;;
		_=> nrel_else:: .._proc_of_finding_vertexes_of_lower_level_operator_D4_erase_all_taxonomy_relation_set;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_G2 (*
		<-_ eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._proc_of_finding_vertexes_of_lower_level_operator_G3;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_D4_erase_all_taxonomy_relation_set (*
		<-_ eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _all_taxonomy_relation;;

		_=> nrel_goto:: .._proc_of_finding_vertexes_of_lower_level_operator_D5_erase_uncheck_lower_level_vertexes_set;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_D5_erase_uncheck_lower_level_vertexes_set (*
		<-_ eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _uncheck_lower_level_vertexes;;

		_=> nrel_goto:: .._proc_of_finding_vertexes_of_lower_level_operator_return;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_return (*
		<-_ return;;

	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_G3 (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _ontology;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _lower_level_vertex;;

		_=> nrel_then:: .._proc_of_finding_vertexes_of_lower_level_operator_G4;;
		_=> nrel_else:: .._proc_of_finding_vertexes_of_lower_level_operator_G1_loop_enter;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_G4 (*
		<-_ genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _vertexes_of_lower_level;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _lower_level_vertex;;

		_=> nrel_goto:: .._proc_of_finding_vertexes_of_lower_level_operator_G1_loop_enter;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_D2_loop_arc_erase (*
		<-_ eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._proc_of_finding_vertexes_of_lower_level_operator_D3_is_quasybinary_relation;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_D3_is_quasybinary_relation (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: quasybinary_relation;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _concrete_taxonomy_relation;;

		_=> nrel_then:: .._proc_of_finding_vertexes_of_lower_level_operator_F1_gen_temp_set;;
		_=> nrel_else:: .._proc_of_finding_vertexes_of_lower_level_operator_E1;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_F1_gen_temp_set (*
		<-_ genEl;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: _set_of_ligaments;;

		_=> nrel_goto:: .._proc_of_finding_vertexes_of_lower_level_operator_F1;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_E1 (*
		<-_ searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _current_node;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _vertex_of_lower_level;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_var:: _concrete_taxonomy_relation;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_3:: _uncheck_lower_level_vertexes;;

		_=> nrel_goto:: .._proc_of_finding_vertexes_of_lower_level_operator_D1_loop_enter;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_F1 (*
		<-_ searchSetStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _ligament;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _current_node;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_var:: _concrete_taxonomy_relation;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_1:: _set_of_ligaments;;

		_=> nrel_then:: .._proc_of_finding_vertexes_of_lower_level_operator_F2_loop_enter;;
		_=> nrel_else:: .._proc_of_finding_vertexes_of_lower_level_operator_F5_erase_temp_set;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_F2_loop_enter (*
		<-_ searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _set_of_ligaments;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _concrete_ligament;;

		_=> nrel_then:: .._proc_of_finding_vertexes_of_lower_level_operator_F3_loop_arc_erase;;
		_=> nrel_else:: .._proc_of_finding_vertexes_of_lower_level_operator_F5_erase_temp_set;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_F5_erase_temp_set (*
		<-_ eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _set_of_ligaments;;

		_=> nrel_goto:: .._proc_of_finding_vertexes_of_lower_level_operator_D1_loop_enter;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_F3_loop_arc_erase (*
		<-_ eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._proc_of_finding_vertexes_of_lower_level_operator_F4;;
	*);;

	_-> .._proc_of_finding_vertexes_of_lower_level_operator_F4 (*
		<-_ searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _concrete_ligament;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _lower_level_vertex;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_3:: _uncheck_lower_level_vertexes;;

		_=> nrel_goto:: .._proc_of_finding_vertexes_of_lower_level_operator_F2_loop_enter;;
	*);;

*);;

*];;
*];;
