agent_of_calculation_average_depth_of_subject_domain
=> nrel_main_idtf:
	[агентная scp-программа вычисления cредней глубины предметных областей] (* <- lang_ru;; *);
	[agent scp-program of calculation average depth of subject domain] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_calculation_average_depth_of_subject_domain = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_calculacaltion_average_depth_of_subject_domain_operator1A (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_calculation_average_depth_of_subject_domain_operator1B;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator1B (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_calculate_average_depth_of_subject_domain;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_then:: .._agent_of_calculation_average_depth_of_subject_domain_operator1C;;
		_=> nrel_else:: .._agent_of_calculation_average_depth_of_subject_domain_operator_return;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator1C (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _subject_domain;;

		_=> nrel_then:: .._agent_of_calculation_average_depth_of_subject_domain_operator1D;;
		_=> nrel_else:: .._agent_of_calculation_average_depth_of_subject_domain_operator_return;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator_return (*
		_<- return;;

	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator1D (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: subject_domain;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _subject_domain;;

		_=> nrel_then:: .._agent_of_calculation_average_depth_of_subject_domain_operator2A;;
		_=> nrel_else:: .._agent_of_calculation_average_depth_of_subject_domain_operator_call_proc_of_adding_validating_message;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator2A (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _subject_domain;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _elem;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_private_subject_domain;;

		_=> nrel_then:: .._agent_of_calculation_average_depth_of_subject_domain_operator2B;;
		_=> nrel_else:: .._agent_of_calculation_average_depth_of_subject_domain_operator2C;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator_call_proc_of_adding_validating_message (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_validating_message;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_calculation_average_depth_of_subject_domain_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [Wrong first parameter! It must be an instance of subject domain];;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr3;;

		_=> nrel_goto:: .._agent_of_calculation_average_depth_of_subject_domain_operator4B;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator4B (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr3;;

		_=> nrel_goto:: .._agent_of_calculation_average_depth_of_subject_domain_operator_return;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator2B (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: _all_depth;;

		_=> nrel_goto:: .._agent_of_calculation_average_depth_of_subject_domain_operator2D;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator2C (*
		_<- varAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _average_value;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [1];;

		_=> nrel_goto:: .._agent_of_calculation_average_depth_of_subject_domain_operator4A;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator4A (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_generation_of_metrics_answer;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_calculation_average_depth_of_subject_domain_param1 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: action_to_calculate_average_depth_of_subject_domain;;
			_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _average_value;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr3;;

		_=> nrel_goto:: .._agent_of_calculation_average_depth_of_subject_domain_operator4B;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator2D (*
		_<- varAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _current_depth;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [0];;

		_=> nrel_goto:: .._agent_of_calculation_average_depth_of_subject_domain_operator2E;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator2E (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_calculation_all_depths_of_subject_domain;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_calculation_average_depth_of_subject_domain_param2 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _subject_domain;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _current_depth;;
			_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _all_depth;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_calculation_average_depth_of_subject_domain_operator2F;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator2F (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_calculation_average_depth_of_subject_domain_operator3A;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator3A (*
		_<- varAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _average_value;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [0];;

		_=> nrel_goto:: .._agent_of_calculation_average_depth_of_subject_domain_operator3B;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator3B (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_calculation_average_value;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_calculation_average_depth_of_subject_domain_param3 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _all_depth;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _average_value;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_of_calculation_average_depth_of_subject_domain_operator3C;;
	*);;

	_-> .._agent_of_calculation_average_depth_of_subject_domain_operator3C (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_of_calculation_average_depth_of_subject_domain_operator4A;;
	*);;

*);;

*];;
