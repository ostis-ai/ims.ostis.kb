agent_of_forming_terminological_ontology
=> nrel_main_idtf:
	[агентная scp-программа формирования терминологической онтологии для заданной предметной области] (* <- lang_ru;; *);
	[agent scp-program of forming terminological ontology for given subject domain] (* <- lang_en;; *);
<- agent_scp_program;;

scp_program -> agent_of_forming_terminological_ontology
	(*
	-> rrel_params: .agent_of_forming_terminological_ontology_params
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	-> rrel_operators: .agent_of_forming_terminological_ontology_operator_set
		(*
		-> rrel_init: .agent_of_forming_terminological_ontology_operator1
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_of_forming_terminological_ontology_operator2;;
			*);;

		-> .agent_of_forming_terminological_ontology_operator2
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: action_of_forming_terminological_ontology;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_then: .agent_of_forming_terminological_ontology_operator3;;
			=> nrel_else: .agent_of_forming_terminological_ontology_operator_return;;
			*);;

		-> .agent_of_forming_terminological_ontology_operator3
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _param;;

			=> nrel_then: .agent_of_forming_terminological_ontology_operator4;;
			=> nrel_else: .agent_of_forming_terminological_ontology_operator_return;;
			*);;

		-> .agent_of_forming_terminological_ontology_operator4
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: subject_domain;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _param;;

			=> nrel_then: .agent_of_forming_terminological_ontology_operator9;;
			=> nrel_else: .agent_of_forming_terminological_ontology_call_proc_of_adding_validating_message;;
			*);;

		-> .agent_of_forming_terminological_ontology_call_proc_of_adding_validating_message
			(*
		  	<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_adding_validating_message;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_forming_terminological_ontology_call_proc_of_adding_validating_message_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
				-> rrel_2: rrel_fixed: rrel_scp_const: [Wrong parameter! Node supplied as a parameter to the input is not a subject domain];;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_forming_terminological_ontology_call_proc_of_adding_validating_message_waitreturn;;
			*);;

		-> .agent_of_forming_terminological_ontology_call_proc_of_adding_validating_message_waitreturn
			(*
		  	<- waitReturn;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_forming_terminological_ontology_operator_return;;
			*);;

		-> .agent_of_forming_terminological_ontology_operator9
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _answer_of_forming_terminological_ontology;;

			=> nrel_goto: .agent_of_forming_terminological_ontology_operator10;;
			*);;

		-> .agent_of_forming_terminological_ontology_operator10
			(*
			<- call;;

			-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_forming_terminological_ontology;;
			-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_forming_terminological_ontology_operator10_params
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _param;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _answer_of_forming_terminological_ontology;;
				*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr2;;

			=> nrel_goto: .agent_of_forming_terminological_ontology_operator11;;
			*);;

		-> .agent_of_forming_terminological_ontology_operator11
			(*
			<- waitReturn;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _descr2;;

			=> nrel_goto: .agent_of_forming_terminological_ontology_operator_gen_answer;;
			*);;

		-> .agent_of_forming_terminological_ontology_operator_gen_answer
			(*
			<- genElStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _answer_of_forming_terminological_ontology;;
			-> rrel_4: rrel_assign: rrel_const: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;

			=> nrel_goto: .agent_of_forming_terminological_ontology_operator_return;;
			*);;

		-> .agent_of_forming_terminological_ontology_operator_return
			(*
			<- return;;
			*);;
		*);;
	*);;
