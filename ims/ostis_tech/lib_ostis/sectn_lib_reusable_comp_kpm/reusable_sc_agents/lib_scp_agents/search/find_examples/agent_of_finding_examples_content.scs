agent_of_finding_examples
=> nrel_main_idtf:
	[агентная scp-программа поиска примеров для заданного понятия] (* <- lang_ru;; *);
	[agent scp-program of searching examples for the given concept] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_finding_examples = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_finding_examples_operator1 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_finding_examples_operator2;;
	*);;

	_-> .._agent_of_finding_examples_operator2 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_of_finding_examples;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_then:: .._agent_of_finding_examples_operator3;;
		_=> nrel_else:: .._agent_of_finding_examples_operator_return;;
	*);;

	_-> .._agent_of_finding_examples_operator3 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _param;;

		_=> nrel_then:: .._agent_of_finding_examples_operator3_find_context;;
		_=> nrel_else:: .._agent_of_finding_examples_operator_return;;
	*);;

	_-> .._agent_of_finding_examples_operator_return (*
		_<- return;;

	*);;

	_-> .._agent_of_finding_examples_operator3_find_context (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _context;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_context_of_action;;

		_=> nrel_then:: .._agent_of_finding_examples_operator3_set_context_existence_to_true;;
		_=> nrel_else:: .._agent_of_finding_examples_operator3_set_context_existence_to_false;;
	*);;

	_-> .._agent_of_finding_examples_operator3_set_context_existence_to_true (*
		_<- contAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_goto:: .._agent_of_finding_examples_operator4A;;
	*);;

	_-> .._agent_of_finding_examples_operator3_set_context_existence_to_false (*
		_<- contAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [false];;

		_=> nrel_goto:: .._agent_of_finding_examples_operator4A;;
	*);;

	_-> .._agent_of_finding_examples_operator4A (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _answer_finding_examples;;

		_=> nrel_goto:: .._agent_of_finding_examples_operator4B_is_context_exist;;
	*);;

	_-> .._agent_of_finding_examples_operator4B_is_context_exist (*
		_<- stringIfEq;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_then:: .._agent_of_finding_examples_operator4B_context_logic_1;;
		_=> nrel_else:: .._agent_of_finding_examples_operator4B;;
	*);;

	_-> .._agent_of_finding_examples_operator4B_context_logic_1 (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _node;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_example;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_3:: _all_examples;;

		_=> nrel_then:: .._agent_of_finding_examples_operator4B_context_logic_2;;
		_=> nrel_else:: .._agent_of_finding_examples_operator_return;;
	*);;

	_-> .._agent_of_finding_examples_operator4B (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _node;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_example;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_3:: rrel_set_4:: _answer_finding_examples;;

		_=> nrel_then:: .._agent_of_finding_examples_operator4C;;
		_=> nrel_else:: .._agent_of_finding_examples_operator_return;;
	*);;

	_-> .._agent_of_finding_examples_operator4C (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_finding_examples;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: rrel_example;;

		_=> nrel_goto:: .._agent_of_finding_examples_operator4D;;
	*);;

	_-> .._agent_of_finding_examples_operator4D (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_finding_examples;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param;;

		_=> nrel_goto:: .._agent_of_finding_examples_operator_gen_answer_finding_examples;;
	*);;

	_-> .._agent_of_finding_examples_operator_gen_answer_finding_examples (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _answer_finding_examples;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_answer;;

		_=> nrel_goto:: .._agent_of_finding_examples_operator_return;;
	*);;

	_-> .._agent_of_finding_examples_operator4B_context_logic_2 (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_finding_intersection_of_two_sets;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_finding_examples_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _all_examples;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _context;;
			_-> rrel_3:: rrel_assign:: rrel_scp_var:: _set_of_examples;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_finding_examples_operator4B_context_logic_3;;
	*);;

	_-> .._agent_of_finding_examples_operator4B_context_logic_3 (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_finding_examples_operator4B_context_logic_4;;
	*);;

	_-> .._agent_of_finding_examples_operator4B_context_logic_4 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _set_of_examples;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _example;;

		_=> nrel_then:: .._agent_of_finding_examples_operator4B_context_logic_5;;
		_=> nrel_else:: .._agent_of_finding_examples_operator4B_context_logic_8;;
	*);;

	_-> .._agent_of_finding_examples_operator4B_context_logic_5 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._agent_of_finding_examples_operator4B_context_logic_6;;
	*);;

	_-> .._agent_of_finding_examples_operator4B_context_logic_8 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _set_of_examples;;

		_=> nrel_goto:: .._agent_of_finding_examples_operator4C;;
	*);;

	_-> .._agent_of_finding_examples_operator4B_context_logic_6 (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _example;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_example;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_4:: _answer_finding_examples;;

		_=> nrel_then:: .._agent_of_finding_examples_operator4B_context_logic_7;;
		_=> nrel_else:: .._agent_of_finding_examples_operator4B_context_logic_4;;
	*);;

	_-> .._agent_of_finding_examples_operator4B_context_logic_7 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_finding_examples;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _example;;

		_=> nrel_goto:: .._agent_of_finding_examples_operator4B_context_logic_4;;
	*);;

*);;

*];;
